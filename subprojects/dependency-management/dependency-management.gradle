apply plugin: "groovy"

sourceCompatibility = 1.6

configurations {
    mvn3Input
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile project(":core")
    compile project(":resources")
    compile libraries.commons_lang
    compile libraries.commons_io
    compile libraries.ivy
    compile libraries.slf4j_api
    compile libraries.gson
    compile libraries.jcip

    runtime libraries.xbean //maven3 classes dependency
    runtime libraries.bouncycastle_provider
    runtime project(":installationBeacon")

    compile libraries.maven3

    testCompile libraries.nekohtml

    integTestImplementation project(":resourcesHttp")

    integTestRuntime project(":ivy")
    integTestRuntime project(":maven")
    integTestRuntime project(":resourcesS3")
    integTestRuntime project(":resourcesSftp")
    //this dependency is necessary to run IvySFtpResolverIntegrationTest on ibm jdk
    //integTestRuntime "org.bouncycastle:bcprov-jdk15:1.46@jar"

    testFixturesCompile project(":internalIntegTesting")
}

useTestFixtures()
useTestFixtures(project: ":messaging")
useTestFixtures(project: ":modelCore")

verifyTestFilesCleanup.errorWhenNotEmpty = false

ext.runtimeShadedPath = "$buildDir/generated-resources/main/org/gradle/api/internal/runtimeshaded"

task configureGenerateGradleApiPackageList {
    doLast {
        generateGradleApiPackageList.configure {
            classpath = files(
                rootProject.configurations.externalModules,
                [':core', ':dependencyManagement', ':pluginUse', ":toolingApi"].collect() {
                    project(it).configurations.runtime
                }, project(':').configurations.gradlePlugins)
        }
    }
}

task generateGradleApiPackageList(type: org.gradle.api.internal.runtimeshaded.PackageListGenerator) {
    dependsOn configureGenerateGradleApiPackageList
    outputFile = file("$runtimeShadedPath/api-relocated.txt")
}

task configureGenerateTestKitPackageList {
    doLast {
        generateTestKitPackageList.configure {
            classpath = project(':testKit').configurations.compile
        }
    }
}

task generateTestKitPackageList(type: org.gradle.api.internal.runtimeshaded.PackageListGenerator) {
    dependsOn configureGenerateTestKitPackageList
    outputFile = file("$runtimeShadedPath/test-kit-relocated.txt")
}

jar.dependsOn(generateGradleApiPackageList, generateTestKitPackageList)

